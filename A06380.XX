	.TITLE RAT3
TRKTAB	=26712
OILS	=27326
HOFF	=27406
VOFF	=27446
ATAB2	=27506
ATAB1	=27606
SLSD1	=2135
SMSD1	=2134
SHSD1	=2133
SLSD2	=2104
SMSD2	=2103
SHSD2	=2102
TLSD	=2120
TMSD	=2117
THSD	=2116
PF	=2000
OF	=2200
SBO1	=6060
SBO2	=6100
GP	=4050
SBI1	=4054
SBI2	=4055
SST	=4052
TSEL	=4056
LFREQ	=4057
GTIME	=4063
SPARE	=4060
GEARAD	=4030
MODE	=4061
BONUS	=4062
COINAD	=4100
STR1	=4200
STR2	=4300
VEL	=0
DIRECT	=4
HPOSL	=10
VPOSL	=14
HPOS	=20
SOUND	=24
VPOS	=30
ROTATE	=31
CREDIT	=40
TEMP1	=42
TEMP2	=44
TEMP3	=46
TEMP4	=50
TEMP5	=52
AUDY	=54
FRAME	=60
FLAGZ	=61
FLAGQ	=62
FLAGH	=63
TRKN	=65
COINS	=66
TOGGLE	=67
TFLAG	=70
EXTEND	=72
TCOUNT	=74
FLAGD	=75
TSAVE	=77
HPOST	=100
VPOST	=104
TH1	=110
TH2	=112
TH3	=114
TH4	=116
TSP	=120
CFLAG	=121
LFLAG	=122
FLAGF	=124
BCD3	=126
THIST	=127
BCD1	=130
BCD2	=131
GEAR	=132
PMODE	=134
LOC	=176
SK1	=6020
SK2	=6040
HIT1	=10000
HIT2	=12000
STRES1	=7000
STRES2	=7200
CRES1	=6400
CRES2	=6600
TREST	=6200
NREST	=7400
RTEST	=141
AMODE	=6000
CRASH	=26
TEST	=14000
TESTV	=252
	.ASECT
.	=30000
NMI:	PHA
	TXA
	PHA
	CLD
	LDA	Z,FRAME
	BNE	S,+5
	STA	A,NREST			;RESET NOISE CIRCUIT
	BIT	A,SST
	BPL	NM1
	LDA	Z,RTEST
	CMP	I,222			;1ST TIME?
	BNE	N1			;YES
	LDA	Z,RTEST-1
	CMP	I,77
	BNE	N1
	TSX
	LDA	AX,405			;GET PCH
	CMP	I,40			;VALID RANGE?
	BCS	NM1			;YES
	CMP	I,100
	BCC	NM1
	CMP	I,20
	BCC	PWRON
	CMP	I,30
	BCS	PWRON
NM1:	PLA
	TAX
	PLA
	RTI				;RETURN
N1:	LDA	I,222			;INITIALIZE RAM LOCATION
	STA	Z,RTEST
	LDA	I,77
	STA	Z,RTEST-1
PWRON:	SEI
	CLD
	LDX	I,177
	TXS				;SET STACK POINTER=7F
	LDA	A,TEST
	CMP	I,TESTV			;DIAGNOSTIC PRESENT?
	BEQ	TESTVJ			;YES, GO TO TEST
	JMP	A,MAINT			;GO TO MAIN LOOP
TESTVJ:	JSR	A,TEST
MAINT:	LDX	I,0			;BLANK 1ST 2 LINES
	JSR	A,BLANK
	LDX	I,20
	JSR	A,BLANK
	LDX 	I,50
	JSR	A,BLANK
	JSR	A,SETUP			;INITIALIZE EVERYTHING
MAIN:	JSR	A,ATRACT		;GO TO ATTRACT MODE
	JSR	A,START			;START GAME
	JMP	A,MAIN
;ATTRACT SUBROUTINE
ATRACT:	LDA	I,0
	STA	Z,TRKN
	STA	A,AMODE+1		;TURN OFF SOUND
AAA:	JSR	A,TRACK			;OUTPUT INITIAL TRACK
	JSR	A,INITIL		;SET UP CARS
	JSR	A,COIN
	LDA	I,0
	LDX	Z,CFLAG
	JSR	A,MESS
	LDA	I,40
	LDX	I,0
A8:	STA	AX,2056
	INX
	CPX	I,4
	BNE	A8
	LDX 	I,38.
	LDA	I,1
	JSR	A,MESS
	LDX	I,12
	LDA	I,0
	JSR	A,MESS
	LDA	Z,TRKN
	BNE	A3
	LDA	I,0
	LDX	I,24.
	JSR	A,MESS
A3:	INC	Z,FRAME
	LDA	Z,FRAME
	BNE	A5
	INC	Z,TRKN
	LDA	Z,TRKN
	CMP	I,14
	BNE	S,+9.
	LDA	I,0
	STA	Z,TRKN
	JMP	A,AAA
	BIT	A,SPARE
	BVC	A5
	JSR	A,TRACK
A5:	BIT	A,SST			;SIMPLE SELF-TEST?
	BMI	S,+5
	JMP	A,SLT			;YES, GO TO SLT
	JSR	A,TRANS
	LDX	I,0			;CAR #
A1:	JSR	A,CNTRL
	INX				;COMPUTER-CONTROL IT
	CPX	I,4
	BNE	A1
	LDA	I,0
	STA	A,CRASH
	STA	A,SOUND
	STA	A,SOUND+1
	STA	A,SK1
	STA	A,SK2
A2:	BIT	A,AMODE			;WAIT FOR V RESET
	BVC	A2
	STA	A,TREST
	JSR	A,COIN			;LOOK FOR COINS
	LDA	Z,COINS			;ANY COINS?
	BNE	A4			;YES
	LDA	Z,CREDIT		;ANY CREDIT?
	BEQ	A3			;NO, CONTINUE ATTRACTING
A4:	JSR	A,LINE
	LDA	Z,COINS
	BNE	S,+6
	LDA	Z,CREDIT
	BEQ	A3
	RTS				;YES, GO TO MAINLINE
;
;START PLAY SUBROUTINE
;
START:	NOP
S1:	LDA	I,0
	STA	Z,TRKN
	JSR	A,TRACK
	LDX	I,20
	JSR	A,BLANK
	LDA	I,0
	LDX	Z,CFLAG
	JSR	A,MESS
	LDA	I,0
	STA	Z,TEMP5
	JSR	A,INITIL		;SET-UP CARS
	JSR	A,TRANS
	LDA	Z,CFLAG
	CMP	I,4
	BNE	S200
SM5:	JSR	A,COIN			;2 COINS PER PLAYER
	STA	A,TREST
	LDA	Z,CREDIT		;ANY CREDIT?
	BEQ	SM5			;NO, LOOK FOR ANOTHER COIN
S200:	STA	A,SBO1+1		;LITE 1-PLAYER START
	LDA	I,0
	STA	Z,CREDIT
	LDX	I,16
	JSR	A,MESS
	LDA	I,0
	LDX	I,14
	JSR	A,MESS
	LDA	Z,CFLAG
	CMP	I,6
	BNE	S201			;2 PLAYERS PER COIN OR FREE PLAY?
	STA	A,SBO2+1		;YES, LITE 2-PLAYER START
S201:	JSR	A,TBONE			;LOOK FOR TRACK SELECT
	STA	A,TREST
	LDA	Z,CFLAG
	CMP	I,6
	BNE	S2010			;2 PLAYS PER COIN OR FREE PLAY?
	BIT	A,SBI2			;YES, LOOK FOR 2-PLAYER START
	BPL	S202
S203:	BIT	A,SBI1			;LOOK FOR 1-PLAYER START
	BMI	S201			;NO
	STA	A,SBO2			;MAKE SURE 2-PLAYER START LITE IS OUT
	LDA	I,1			;YES, SET PMODE=1
	BPL	S2020
S2010:	JSR	A,COIN
	LDA	Z,CREDIT		;ANY MORE CREDIT?
	BEQ	S203			;NO
	STA	A,SBO1			;YES, TURN OFF 1-PLAY START
	STA 	A,SBO2+1		;TURN ON 2-PLAYER START
	BIT	A,SBI2			;2-PLAYER START?
	BMI	S201			;NO
S202:	STA	A,SBO1			;MAKE SURE 1-PLAYER START LITE IS OUT
	LDA	I,2			;YES, SET PMODE=2
S2020:	STA	Z,PMODE
	LDX	I,16
	JSR	A,BLANK
	LDA	Z,EXTEND
	CMP	I,100
	BEQ	S205
	LDA	I,0
	LDX	I,24
	JSR	A,MESS
	LDA	Z,EXTEND
	CMP	I,60
	BEQ	S,+5
	STA	A,3564
	LDA	Z,EXTEND+1
	STA	A,3565
S205:	LDX	I,4
	JSR	A,BLANK
S204:	STA	A,AMODE			;TURN ON SOUND
	JSR	A,TRACK
PLAY:	JSR	A,TRANS
S3:	INC	Z,FRAME			;INCREMENT FRAME COUNT
	LDX	I,0
	JSR	A,STEER
	INX
	CPX	Z,PMODE
	BNE	S,-6
	LDX	Z,PMODE
S31:	JSR	A,CNTRL			;COMPUTER-CONTROL CARS
	INX
	CPX	I,4
	BNE	S31
	LDA	Z,PMODE
	CMP	I,1
	BNE	S,+7
	LDX	I,1
	JSR	A,SCORE
;
;USER CARS
;
;   PLAYFIELD COLLISION
;
	STA	A,SK1			;TURN OFF SKID
	STA	A,SK2
	LDX	I,0
	LDA	I,0
	STA	Z,TEMP1
	LDA	I,20
	BPL	S,+4
S1000:	LDA	I,24
	STA	Z,TEMP1+1
	LDY	I,0
	LDA	NY,TEMP1		;PLAYFIELD HIT?
	BMI	S60			;YES
	LDA	ZX,FLAGD		;DISABLE WALL HIT LOGIC
	BEQ	S60			;FOR 16 FRAMES
	DEC	ZX,FLAGD
	JMP	A,S70
S60:	LDA	NY,TEMP1		;PLAYFIELD HIT?
	BMI	S61
	LDA	I,0			;NO, CLEAR FLAGS
	STA	ZX,FLAGD
	STA	ZX,FLAGF
	JMP	A,S70
S61:	LDA	I,0
	STA	ZX,FLAGH		;DISABLE CAR HITS
	LDA	ZX,FLAGF		;YES, 1ST TIME?
	BNE	S62			;NO
	LDA	Z,PMODE			;YES, SET VEL=1
	STA	ZX,VEL
	STA	ZX,FLAGF
	LDA	I,17			;SET UP CRASH NOISE
	STA	Z,CRASH
S62:	LDA	I,20
	STA	ZX,FLAGD
S70:	LDA	Z,CRASH			;CRASH STILL ACTIVE?
	BEQ	S80			;NO
	LDA	Z,FRAME			;YES, 4TH FRAME?
	LSR
	BCC	S80			;NO
	LSR	
	BCC	S80
	DEC	Z,CRASH			;YES, CONTINUE CRASH
S80:	LDA	ZX,FLAGH		;CAR HIT STILL ACTIVE?
	BEQ	S81			;NO
	CPX	I,0
	BEQ	S,+7
	STA	A,SK2+1
	BNE	S,+5
	STA	A,SK1+1			;YES, KEEP SKID SOUND ON
	DEC	ZX,FLAGH
	TXA
	ASL
	TAY
	LDA	I,2
	CPX	I,0
	BEQ	S,+8.
	BIT	A,STR2
	JMP	A,S82
	BIT	A,STR1
S82:	BVS	S800
	LDA	I,376
S800:	CLC
	ADC	AY,ROTATE
	STA	AY,ROTATE
;
;CAR COLLISION
;
S81:	LDY	I,0
	LDA	NY,TEMP1		;CAR HIT?
	ROL
	BPL	S90			;NO
	LDA	I,40			;YES, SET UP CAR HIT ACTIVE
	STA	ZX,FLAGH
S90:	CPX	I,0
	BEQ	S,+7
	STA	A,CRES2
	BNE	S,+5
	STA	A,CRES1			;COLLISION RESET
;
;ACCELERATION
;
	LDA	Z,FRAME
	LSR
	BCC	S130
	LSR
	BCC	S130
	JSR	A,GETAC			;GET ACCELERATION
	STA	Z,TEMP2
	CMP	I,371			;ACCELERATION.LE.SKID VALUE
	BPL	S10			;NO
	CPX	I,0
	BEQ	S,+7
	STA	A,SK2+1
	BNE	S,+5
	STA	A,SK1+1			;YES, TURN ON SKID
S10:	LDA	Z,TEMP2
	CLC
	ADC	ZX,VEL
	BNE	S131
	LDA	I,1
S131:	STA	ZX,VEL			;VEL=VEL+ACCELERATION
;
;ENGINE RPM SOUND
;
S130:	LDA	ZX,VEL
	LSR				;GET RPM SOUND
	LSR
	LSR
	STA	Z,TEMP1
	LDY	ZX,GEAR
	LDA	ZX,GEAR
	ASL
	STA	Z,TEMP2
	LDA	AY,RATIO
	TAY
	LDA	I,0
S101:	CLC
	ADC	Z,TEMP1
	DEY
	BNE	S101
	LSR
	LSR
	LSR
	SEC
	SBC	Z,TEMP2
	BPL	S102
	LDA	I,0
S102:	CMP	I,20
	BMI	S104
	LDA	I,17
S104:	STA	ZX,SOUND
	TXA
	ASL
	TAY
;
;UPDATE DIRECTION & STEERING
;
	JSR	A,STEER
;
;DIRECTION UPDATE
;
	LDA	ZX,VEL
	EOR	I,377
	LSR
	LSR
	LSR
	LSR
	LSR
	LSR
	AND	I,6
	CLC
	ADC	I,3
	STA	Z,TEMP1			;CONSTANT = COM(VEL(3MSB))+3
	LDA	AY,ROTATE
	AND	I,370
	STA	Z,TEMP2
	LDA	ZX,DIRECT
	AND	I,370
	SEC
	SBC	Z,TEMP2
	BEQ	S20
	BMI	S211
	CMP	I,20
	BMI	S2110
	CPX	I,0
	BEQ	S,+7
	STA	A,SK2+1
	BNE	S,+5
	STA	A,SK1+1			;TURN ON SKID
S2110:	LDA	I,0
	SEC
	SBC	Z,TEMP1
	STA	Z,TEMP1			;CONSTANT=-CONSTANT
	JMP	A,S21
S211:	CMP	I,370
	BPL	S21
	CPX	I,0
	BEQ	S,+7
	STA	A,SK2+1
	BNE	S,+5
	STA	A,SK1+1			;TURN ON SKID
S21:	LDA	Z,TEMP1
	CLC
	ADC	ZX,DIRECT
	STA	ZX,DIRECT		;UPDATE DIRECTION
;
;UPDATE POSITION
;
S20:	JSR	A,UPDATE		;UPDATE USER CAR POSITION
;
;CHECKPOINTS & SCORE
;
	JSR	A,SCORE
;
;EXTENDED PLAY
;
S37:	STX	Z,TEMP5
	LDA	Z,FLAGZ
	BEQ	S2900
	LDA	Z,FRAME
	AND	I,40
	BEQ	S2901
	LDA	I,40
	STA	AX,2051
	INX
	CPX	I,13.
	BNE	S,-6
	LDX	Z,TEMP5
	JMP	A,S2900
S2901:	LDX	I,22
	LDA	I,0
	JSR	A,MESS
S2900:	LDX	Z,TEMP5
	CPX	I,0
	BNE	S,+9.
	LDA	A,SHSD1
	AND	I,77
	BPL	S,+5
	LDA	A,SHSD2
	CMP	Z,EXTEND
	BMI	S378
	CPX	I,0
	BNE	S,+9.
	LDA	A,SMSD1
	AND	I,77
	BPL	S207
	LDA	Z,PMODE			;IF 1-PLAYER, MAKE SURE EXTENDED
	CMP	I,1			;PLAY DOES NOT OCCUR ON COMPUTER CAR
	BEQ	S378
	LDA	A,SMSD2
S207:	CMP	Z,EXTEND+1
	BMI	S378
	LDA	Z,FLAGZ
	BNE	S378
	LDA	Z,FLAGQ
	BNE	S378
	INC	Z,FLAGQ
;
;FINISHED?
;
;
;TIMER
;
S378:	CPX	I,0
	BNE	S252
	DEC	Z,TCOUNT		;TIME TO UPDATE TIME COUNT?
	BNE	S252			;NO
	LDA	Z,TSP
	BNE	S370
	INC	Z,TSP
	LDA	I,40
	STA	A,THSD
	LDA	I,71
	STA	A,TMSD
	LDA	I,72
	STA	A,TLSD
	JMP	A,S251
S370:	LDA	A,TLSD			;YES
	CMP	I,60			;LSD OF TIME=0?
	BNE	S251			;NO
	LDA	A,TMSD			;YES
	CMP	I,60			;MSD OF TIME=0?
	BEQ	S2500			;YES, BYPASS TIME UPDATE
	DEC	A,TMSD			;NO, DECREMENT MSD
	LDA	I,72
	STA	A,TLSD			;SET LSD=9
S251:	DEC	A,TLSD			;DECREMENT LSD
	LDA	Z,TSAVE
	STA	Z,TCOUNT
S252:	JMP	A,S50
S2500:	LDA	Z,FLAGQ
	BEQ	S2501
	LDA	I,0
	STA	Z,FLAGQ
	INC	Z,FLAGZ
	LDA	I,63
	STA	A,TMSD
	LDA	I,0
	STX	Z,TEMP5
	LDX	I,22
	JSR	A,MESS
	LDX	Z,TEMP5
	LDA	Z,TSAVE
	STA	Z,TCOUNT
	JMP	A,S50
S2501:	STA	A,AMODE+1		;TURN OFF SOUND
	STA	A,SK1			;TURN OFF SKID
	STA	A,SK2
	LDA	I,0
	STA	Z,CRASH			;TURN OFF CRASH
	STA	Z,SOUND			;LOWER RPM
	STA	Z,SOUND+1
	LDA	I,1
	LDX	I,20
	JSR	A,MESS
	STA	A,SBO1
	STA	A,SBO2
	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	LDX	I,8.
	JSR	A,BLANK
	LDA	I,1
	LDX	I,8.
	JSR	A,MESS
	LDA	A,SHSD1
	AND	I,17
	ASL
	ASL
	ASL
	ASL
	STA	Z,TEMP1
	LDA	A,SMSD1
	AND	I,17
	ORA	Z,TEMP1
	STA	A,TEMP1
	LDA	Z,TEMP1
	CMP	Z,BCD1
	BMI	B10
	LDX	I,28.
	BPL	B20
B10:	CMP	Z,BCD2
	BMI	B11
	LDX	I,26.		;ROOKIE
	BPL	B20
B11:	LDX	I,22.		;GRANNIE
B20:	LDA	I,1
	JSR 	A,MESS
	LDA	Z,PMODE		;1 - PLAYER?
	CMP	I,1		;YES, BYPASS SECOND RATING
	BEQ	B30
	LDA	A,SHSD2
	AND	I,17
	ASL
	ASL
	ASL
	ASL
	STA	Z,TEMP1
	LDA	A,SMSD2
	AND	I,17
	ORA	Z,TEMP1
	STA	Z,TEMP1
	LDA	Z,TEMP1
	CMP	Z,BCD1
	BMI	B21
	LDX	I,36.		;PRO
	BPL	B29
B21:	CMP	Z,BCD2
	BMI	B22
	LDX	I,34.		;ROOKIE
	BPL	B29
B22:	LDX	I,30.		;GRANNIE
B29:	LDA	I,0
	JSR	A,MESS
;
;FREEZE SCREEN
;
B30:	LDA	I,10			;FREEZE SCREEN FOR 10 SECONDS
	LDX	I,177
	LDY	I,177
S40:	STA	Z,TEMP3
	STX	Z,TEMP4
	STY	Z,TEMP5
	JSR	A,COIN			;LOOK FOR COINS
	LDA	Z,COINS
	BNE	S401
	LDA	Z,CREDIT
	BNE	S401
	BEQ	S400
S400:	LDA	Z,TEMP3			;NO
	LDX	Z,TEMP4
	LDY	Z,TEMP5
	DEY
	BNE	S40
	STA	Z,TEMP5
	STX	Z,TEMP5+1
	INC	Z,FRAME
	LDA	Z,FRAME
	AND	I,40
	BEQ	S,+10.
	LDX	I,20
	JSR	A,BLANK
	JMP	A,S9000
	LDA	I,1
	LDX	I,20
	JSR	A,MESS
S9000:	LDA	Z,TEMP5
	LDX	Z,TEMP5+1
	STA	A,TREST
	LDY	I,177
	DEX
	BNE	S40
	LDY	I,177
	LDX	I,177
	CMP	I,11
	BCC	S,+4
	LDA	I,5
	SEC
	SBC	I,1
	BPL	S40
	LDA	I,1
	STA	Z,TEMP5
S401:	RTS
S41:	JMP	A,S1
S5001:	JMP	A,S1000
S50:	JSR	A,STEER
	INX
	CPX	Z,PMODE
	BNE	S5001
	JSR	A,CHASC
S501:	BIT	A,AMODE			;WAIT FOR V RESET
	BVC	S501
	STA	A,TREST			;PULSE WATCHDOG
	JMP	A,PLAY
;
;
;SUBROUTINES
;
;
CNTRL:	JSR	A,AUTO			;GET AUTO-DIRECTION
	LDA	ZX,VEL
	LSR
	LSR
	LSR
	LSR
	LSR
	CLC
	ADC	I,1
	STA	Z,TEMP1			;SET ACCEL=(3 MSB OF VEL)+1
	LDY	I,3
	LDA	ZX,DIRECT
	SEC
	SBC	ZX,AUDY			;DIRECT-AUDY
	AND	I,370
	BEQ	A6
	BMI	A7
	LDY	I,375
A7:	TYA
	CLC
	ADC	ZX,DIRECT
	STA	ZX,DIRECT
	STX	Z,TEMP3
	ASL	Z,TEMP3
	LDY	Z,TEMP3
	STA	AY,ROTATE
	LDA	I,377
	STA	Z,TEMP1			;SET ACCELERATION=-1
A6:	LDA	ZX,VEL
	CLC
	ADC	Z,TEMP1			;VEL=VEL+ACCELERATION
	CMP	AX,MAXVEL
	BCC	A10
	LDA	AX,MAXVEL
A10:	STA	ZX,VEL
	JSR	A,UPDATE		;UPDATE DIRECTION
	RTS
AUTO:	LDA	I,0
	STA	Z,TEMP1
	LDA	I,04
	STA	Z,TEMP1+1
	LDA	ZX,HPOST
	CLC
	ADC	I,4			;ADD 4 TO GET CENTER OF CAR
	LSR
	LSR
	LSR				;DIVIDE BY 8
	EOR	I,37			;COMPLEMENT IT
	CLC
	ADC	Z,TEMP1
	STA	Z,TEMP1
	LDA	I,0
	STA	Z,TEMP2
	ADC	Z,TEMP1+1
	STA	Z,TEMP1+1
	LDA	ZX,VPOST
	CLC
	ADC	I,4			;ADD 4 TO GET CENTER OF CAR
	ORA	I,7			;OR IN 3 LSB'S
	EOR	I,377			;COMPLEMENT IT
	ASL
	ROL	Z,TEMP2			;SAVE CARRY
	ASL
	ROL	Z,TEMP2			;SAVE 2 MSB'S
	CLC
	ADC	Z,TEMP1
	STA	Z,TEMP1
	LDA	Z,TEMP2
	ADC	Z,TEMP1+1
	STA	Z,TEMP1+1
	LDY	I,0
	LDA	NY,TEMP1		;GET SQUARE
	BPL	AU1
	ASL
	ASL
	ASL
	ASL
	ASL
	STA	ZX,AUDY			;SAVE AUTO-DIRECTION
AU1:	RTS
UPDATE:	STX	Z,TEMP1
	LDA	I,0
	STA	Z,LFLAG
	LDA	ZX,VEL
	BEQ	U3
	LSR
	LSR
	LSR
	STA	Z,TEMP2			;MULTIPLICAND (VEL)
U1:	LDA	ZX,DIRECT
	LSR
	LSR
	LSR
	TAX
	STX	Z,TEMP3
	LDA 	AX,HOFF
	BEQ	U2
	ASL
	TAX				;MULTIPLIER (HOFF)
	JSR	A,MULT			;GET VEL*HOFF
	LDX	Z,TEMP1
	LDA	Z,TEMP4
	CLC
	ADC	ZX,HPOSL
	STA	ZX,HPOSL
	LDA	Z,TEMP4+1
	ADC	ZX,HPOST
	BPL	U5
	CMP	I,374
	BMI	U4
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,0
	BEQ	U5
U4:	CMP	I,367
	BMI	U5
	LDA	I,1
	STA	A,LFLAG
	LDA	I,367
U5:	STA	ZX,HPOST
U2:	LDX	Z,TEMP3
	LDA	AX,VOFF
	BEQ	U3
	ASL
	TAX				;MULTIPLIER (VOFF)
	JSR	A,MULT			;GET VEL*VOFF
	LDX	Z,TEMP1
	LDA	Z,TEMP4
	CLC
	ADC	ZX,VPOSL
	STA	ZX,VPOSL
	LDA	Z,TEMP4+1
	ADC	ZX,VPOST
	BPL	U6
	CMP	I,350
	BMI	U7
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,350
	BMI	U7
U6:	CMP	I,40
	BPL	U7
	LDA	I,1
	STA	Z,LFLAG
	LDA	I,40
U7:	STA	ZX,VPOST
U3:	LDX	Z,TEMP1
	CPX	Z,PMODE
	BMI	U10
	LDA	Z,LFLAG
	BEQ	U10
	LDA	ZX,DIRECT
	EOR	I,40
	STA	ZX,DIRECT
U10:	RTS
MULT:	STX	Z,TEMP2+1
	TXA
	BPL	M3
	EOR	I,377
	CLC
	ADC	I,1
	TAX
M3:	LDA	I,0
	STA	Z,TEMP4
	STA	Z,TEMP4+1
M2:	LDA	Z,TEMP4
	CLC
	ADC	Z,TEMP2
	STA	Z,TEMP4
	LDA	I,0
	ADC	Z,TEMP4+1
	STA	Z,TEMP4+1
	DEX
	BNE	M2
	LDA	Z,TEMP2+1
	BPL	M1
	LDA	I,0
	SEC
	SBC	Z,TEMP4
	STA	Z,TEMP4
	LDA	I,0
	SBC	Z,TEMP4+1
	STA	Z,TEMP4+1
M1:	RTS
INITIL:	LDX	I,0			;SET INITIAL CAR POSITIONS
	LDA	I,40
	LDY	I,0
I1:	STA	ZX,HPOST
	STY	ZX,HPOSL
	STY	ZX,VPOSL
	INX
	CPX	I,4
	BNE	I1
	LDX	I,0
	LDA	I,1
I3:	STA	ZX,VEL			;SET VELOCITIES=1
	INX
	CPX	I,4
	BNE	I3
	LDX	I,0
	LDY	I,0
	LDA	I,300
I2:	STA	ZX,DIRECT		;SET UP DIRECTIONS' ROTATION
	STA	AY,ROTATE
	INY
	INY
	INX
	CPX	I,4
	BNE	I2
	LDA	I,54
	STA	Z,VPOST+3
	LDA	I,70
	STA	Z,VPOST+2
	LDA	I,104
	STA	Z,VPOST+1
	LDA	I,120
	STA	Z,VPOST
	LDA	A,GTIME			;SET-UP GAME TIME
	EOR	I,377			;ACCORDING TO SWITCHES
	CLC
	ROL
	ROL
	ROL
	AND	I,3
	TAY
	LDA	AY,TUP
	STA	Z,TCOUNT
	STA	Z,TSAVE
	LDA	I,0
	LDX	I,0
I5:	STA	ZX,FLAGH
	STA	ZX,FLAGD
	STA	ZX,FLAGF
	INX
	CPX	I,2
	BNE	I5
	LDA	I,0			;INITIALIZE SOUND & FRAME COUNT
	STA	Z,TOGGLE
	STA	Z,FRAME
	STA	Z,THIST
	STA	Z,FLAGQ
	STA	Z,FLAGZ
	STA	A,SK1
	STA	A,SK2
	STA	Z,SOUND
	STA	Z,SOUND+1
	STA	Z,TFLAG
	STA	Z,TSP
	STA	A,CRASH
	STA	A,CRES1			;DO A COLLISION RESET
	STA	A,CRES2
	STA	A,STRES1		;DO A STEERING RESET
	STA	A,STRES2
	LDA	I,1
	STA	Z,PMODE
	LDA	I,40
	LDX	I,0
I7:	STA	AX,2113
	INX
	CPX	I,10.
	BNE	I7
	LDA	I,0
	LDX	I,0			;OUTPUT "SCORE"
	JSR	A,MESS
	LDA	Z,TEMP5
	BNE	I6
	LDA	I,260			;SET SCORE=000
	STA	A,SLSD1
	STA	A,SMSD1
	STA	A,SHSD1
	LDA	I,60
	STA	A,SLSD2
	STA	A,SMSD2
	STA	A,SHSD2
I6:	LDA	I,60
	STA	A,TMSD
	STA	A,TLSD			;SET TIME=100
	LDA	I,61
	STA	A,THSD
	RTS
SETUP:	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	STA	A,SBO1
	STA	A,SBO2
	STA	Z,TEMP5
	LDA	I,2
	STA	Z,CFLAG
	RTS
GETAC:	STX	Z,TEMP3
	LDA	I,0
	STA	ZX,GEAR
	LDA	AX,GEARAD
	BPL	G9
	INC	ZX,GEAR
	LDA	AX,GEARAD+2
	BPL	G9
	INC	ZX,GEAR
	LDA	AX,GEARAD+4
	BPL	G9
	INC	ZX,GEAR
G9:	LDA	ZX,VEL
	AND	I,370
	LSR
	LSR
	STA	Z,TEMP4
	LDA	ZX,GEAR
	AND	I,2
	BEQ	G2
	INC	Z,TEMP4
G2:	LDA	AX,GP			;GAS PEDAL SET?
	BPL	G4
	LDX	Z,TEMP4
	LDA	AX,ATAB2
	JMP	A,G5
G4:	LDX	Z,TEMP4			;NO
	LDA	AX,ATAB1
G5:	LDX	Z,TEMP3
	CLC
	LSR	ZX,GEAR			;UPPER OR LOWER HALF BYTE?
	BCS	G6
	AND	I,360
	LSR
	LSR
	LSR
	LSR
G6:	AND	I,17
	STA	Z,TEMP2
	ROL	ZX,GEAR
	LDA	I,10
	BIT	Z,TEMP2
	BEQ	G8
	LDA	Z,TEMP2
	ORA	I,360
	STA	Z,TEMP2
G8:	LDA	Z,TEMP2
	LDX	Z,TEMP3
	RTS
;
;STEERING
;
STEER:	TXA
	ASL
	TAY
	LDA	ZX,FLAGH
	BNE	S15
	CPX	I,0
	BEQ	S,+8.
	BIT	A,STR2
	JMP	A,S83
	BIT	A,STR1			;ANY CHANGE IN STEERING?
S83:	BMI	S15			;NO, BYPASS ROTATION UPDATE
	LDA	I,3
	STA	Z,TEMP1
	CPX	I,0
	BEQ	S,+8.
	BIT	A,STR2
	JMP	A,S84
	BIT	A,STR1
S84:	BVS	S16
	LDA	I,375
	STA	Z,TEMP1
S16:	LDA	AY,ROTATE
	CLC
	ADC	Z,TEMP1
	STA	AY,ROTATE
	CPX	I,0
	BEQ	S,+7
	STA	A,STRES2
	BNE	S,+5
	STA	A,STRES1		;CLEAR STEERING FLAG
S15:	RTS
;
;COIN ROUTINE
;
COIN:	JSR	A,KOIN			;GET COINS
	LDA	I,6
	STA	Z,CFLAG
	BIT	A,MODE			;FIND COIN MODE
	BPL	C50
	BVS	C53			;2 COINS 1 CREDIT OR FREE PLAY
	LDA	I,4
	STA	Z,CFLAG
	LDA	Z,COINS			;2 COINS, 1 PLAY
	CMP	I,2
	BEQ	S,+3
	RTS
	DEC	Z,COINS
	BPL	C53
C50:	BVS	C54
	LDA	I,2
	STA	Z,CFLAG
C54:	LDA	Z,COINS
	BNE	S,+3
	RTS
	BVC	C53			;1 COIN 1 PLAY OR 1 COIN 2 PLAYS
	INC	Z,CREDIT
C53:	INC	Z,CREDIT
	DEC	Z,COINS
	RTS
KOIN:	LDA	I,17
	STA	Z,TEMP1
	STA	Z,TEMP2
C1:	BIT	A,COINAD		;ANY COINS?
	BPL	S,+5
	BVC	S,+3
	RTS
	STA	A,TREST
	LDY	I,372			;1.5 MSEC DELAY
	DEY
	BNE	S,-1
	BIT	A,COINAD		;COIN 1?
	BVC	S,+8.
	LDA	I,17
	STA	Z,TEMP1
	BPL	C2
	LDA	Z,TEMP1
	BEQ	C2
	DEC	Z,TEMP1
	BNE	C2
	INC	Z,COINS
C2:	BIT	A,COINAD		;COIN 2?
	BPL	S,+8.			;YES
	LDA	I,17			;NO
	STA	Z,TEMP2
	BPL	CEXTRA
	LDA	Z,TEMP2
	BEQ	CEXTRA
	DEC	Z,TEMP2
	BNE	CEXTRA
	INC	Z,COINS
CEXTRA:	JMP	A,ZEXTRA
MESS:	LDY	I,0
	CMP	I,0
	BEQ	S,+4
	LDY	I,200
	STY	Z,TEMP4
	LDA	AX,MESG
	STA	Z,TEMP1
	LDA	AX,MESG+1
	STA	Z,TEMP1+1
	LDA	AX,POSM
	STA	Z,TEMP2
	LDA	AX,POSM+1
	STA	Z,TEMP2+1
	LDY	I,0
M100:	LDA	NY,TEMP1
	BEQ	M200
	ORA	Z,TEMP4
	STA	NY,TEMP2
	INY
	JMP	A,M100
M200:	RTS
BLANK:	LDA	AX,POSM			;BLANK ONE LINE
	STA	Z,TEMP1
	LDA	AX,POSM+1
	STA	Z,TEMP1+1
	LDA	I,40
	LDY	I,0
B1:	STA	NY,TEMP1
	INY
	CPY	I,40
	BNE	B1
	RTS
TRACK:	LDA	Z,TRKN
	BNE	T6
	LDX	I,52			;IF INITIAL TRACK,
T7:	JSR	A,BLANK			;BLANK AREA
	INX
	INX
	CPX	I,66
	BNE	T7
	LDA	Z,TRKN
T6:	ASL
	TAX
	LDA	AX,TADD
	STA	Z,TEMP2
	LDA	AX,TADD+1
	STA	Z,TEMP2+1
	LDY	I,0
T1:	LDA	NY,TEMP2
	STA	AY,TH1
	INY
	CPY	I,6
	BNE	T1
T2:	JSR	A,COIN
	LDY	I,0
	LDA	NY,TH1
	AND	I,360
	LSR
	LSR
	LSR
	LSR
	ORA	I,240
	STA	NY,TH3
	LDA	Z,TH3
	CLC
	ADC	I,1
	STA	Z,TH3
	LDA	I,0
	ADC	Z,TH3+1
	STA	Z,TH3+1
	LDA	NY,TH1
	AND	I,17
	ORA	I,240
	STA	NY,TH3
	LDA	Z,TH1
	CLC
	ADC	I,1
	STA	Z,TH1
	LDA	I,0
	ADC	Z,TH1+1
	STA	Z,TH1+1
	LDA	Z,TH3
	CLC
	ADC	I,1
	STA	Z,TH3
	LDA	I,0
	ADC	Z,TH3+1
	STA	Z,TH3+1
	LDA	Z,TH1+1
	CMP	Z,TH2+1
	BEQ	T3
	BNE	T2
T3:	LDA	Z,TH1
	CMP	Z,TH2
	BNE	T2
	BIT	A,SPARE			;OIL SLICK?
	BMI	T8
	LDA	Z,TRKN
	ASL
	ASL
	TAY
	LDX	I,0
T10:	LDA	AY,OILS
	STA	ZX,TEMP1
	INY
	INX
	CPX	I,4
	BNE	T10
	LDX	I,0
T11:	LDY	I,0
	LDA	I,74
	STA	NY,TEMP1
	LDY	I,1
	LDA	I,75
	STA	NY,TEMP1
	LDY	I,40
	LDA	I,76
	STA	NY,TEMP1
	LDY	I,41
	LDA	I,77
	STA	NY,TEMP1
	INX
	CPX	I,1
	BNE	T8
	LDA	Z,TEMP2
	STA	Z,TEMP1
	LDA	Z,TEMP2+1
	STA	Z,TEMP1+1
	JMP	A,T11
T8:	LDX	I,0
	LDA	Z,TRKN
	BEQ	TT9
	INX
	CMP	I,4
	BMI	TT9
	CMP	I,6
	BEQ	TT9
	CMP	I,7
	BEQ	TT9
	INX
TT9:	LDA	AX,PRO
	STA	Z,BCD1
	LDA	AX,ROOKIE
	STA	Z,BCD2
	LDA	AX,NOVICE
	STA	Z,BCD3
	LDA	A,GTIME
	EOR	I,377
	CLC
	ROL
	ROL
	ROL
	AND	I,3
	CLC
	ADC	I,2
	STA	Z,TEMP1
	LDX	I,0
	STX	Z,TEMP2
	STX	Z,TEMP3
	STX	Z,TEMP4
TT10:	LDA	Z,TEMP2
	SED
	CLC
	ADC	Z,BCD1
	STA	Z,TEMP2
	LDA	Z,TEMP3
	CLC
	ADC	Z,BCD2
	STA	Z,TEMP3
	LDA	Z,TEMP4
	CLC
	ADC	Z,BCD3
	STA	Z,TEMP4
	CLD
	INX
	CPX	Z,TEMP1
	BNE	TT10
	LDA	Z,TEMP2
	STA	Z,BCD1
	LDA	Z,TEMP3
	STA	Z,BCD2
	LDA	Z,TEMP4
	STA	Z,BCD3
	STA	A,TREST
	LDA	A,BONUS			;EXTENDED PLAY?
	BPL	TT11
	LDA	I,100
	STA	Z,EXTEND
	STA	Z,EXTEND+1
	LDA	Z,BCD3
	STA	Z,BCD1
	RTS
TT11:	LDA	Z,BCD3
	AND	I,360
	LSR
	LSR
	LSR
	LSR
	ORA	I,60
	STA	Z,EXTEND
	LDA	Z,BCD3
	AND	I,17
	ORA	I,60
	STA	Z,EXTEND+1
	RTS
LINE:	LDA	A,2113
	CMP	I,313
	BNE	LIN1
	LDA	A,MES30
	CMP	I,113
	BNE	LIN1
	BIT	A,LFREQ			;CHECK 50/60 HZ BIT
	BMI	S,+3
	RTS
	BIT	A,AMODE
	BPL	S,-3
	BIT	A,AMODE
	BMI	S,-3
	LDA	I,10.			;WAIT FOR 18.5 MSEC
LAD:	LDY	I,0
	INY
	BNE	S,-1
	SEC
	SBC	I,1
	BNE	LAD
	BIT	A,AMODE			;CHECK POLARITY OF AC
	BMI	LIN1			;IF NEGATIVE, THEN RESET CREDIT
	RTS
LIN1:	LDA	I,0
	STA	Z,CREDIT
	STA	Z,COINS
	RTS
TBONE:	BIT	A,TSEL
	BPL	S,+7
	LDA	I,0
	STA	Z,THIST
	RTS
	LDA	Z,THIST
	BEQ	S,+3
	RTS
	LDA	I,360
	STA	Z,TFLAG
TB1:	LDY	I,372
	DEY
	BNE	S,-1
	BIT	A,TSEL
	BPL	S,+3
	RTS
	INC	Z,TFLAG
	BNE	TB1
	INC	Z,TRKN
	LDA	Z,TRKN
	CMP	I,14
	BNE	S,+4
	LDA	I,0
	STA	Z,TRKN
	JSR	A,TRACK
	LDA	I,1
	STA	Z,THIST
	RTS
CHASC:	LDA	A,SLSD1			;CHECK VALIDITY OF DIGITS
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SLSD1
	LDA	A,SMSD1
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SMSD1
	LDA	A,SHSD1
	AND	I,77
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,260
	STA	A,SHSD1
	LDA	A,SLSD2
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,60
	STA	A,SLSD2
	LDA	A,SMSD2
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,60
	STA	A,SMSD2
	LDA	A,SHSD2
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,60
	STA	A,SHSD2
	LDA	A,TMSD
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,71
	STA	A,TMSD
	LDA	A,TLSD
	JSR	A,ASCK
	LDA	Z,TEMP1
	BEQ	S,+7
	LDA	I,71
	STA	A,TLSD
	RTS
ASCK:	CMP	I,60
	BCC	AS1
	CMP	I,72
	BCS	AS1
	LDA	I,0
	BEQ	S,+4
AS1:	LDA	I,1
	STA	Z,TEMP1
	RTS
;
;CHECKPOINTS
;
SCORE:	LDA	Z,TRKN
	ASL
	TAY
	LDA	AY,TRKTAB		;GET CORRECT TRACK CHECKPOINTS
	STA	Z,TEMP2
	LDA	AY,TRKTAB+1
	STA	Z,TEMP2+1
	CPX	I,0
	BEQ	S,+7
	LDA	A,SLSD2
	BPL	S,+5
	LDA	A,SLSD1
	LSR
	AND	I,7
	ASL
	TAY
	LDA	NY,TEMP2
	STA	Z,TEMP4
	INY
	LDA	NY,TEMP2
	STA	Z,TEMP4+1
	LDA	ZX,VPOST
	AND	I,360
	CLC
	ADC	I,4
	AND	I,360
	STA	Z,TEMP5
	LDA	ZX,HPOST
	LSR
	LSR
	LSR
	LSR
	AND	I,17
	ORA	Z,TEMP5
	STA	Z,TEMP5
	LDY	I,0
S27:	LDA	NY,TEMP4		;GET CORRECT CHECKPOINT
	CMP	Z,TEMP5			;CAR ON CHECKPOINT?
	BEQ	S26			;YES
	INY
	CPY	I,4
	BNE	S27
	JMP	A,S3580
;
;SCORE
;
S26:	CPX	I,0
	BNE	S268
	LDA	A,SLSD1
	CLC
	ADC	I,2
	CMP	I,272
	BNE	S35
	LDA	A,SMSD1
	CLC
	ADC	I,1
	CMP	I,272
	BNE	S266
	LDA	A,SHSD1
	CLC
	ADC	I,1
	STA	A,SHSD1
	LDA	I,260
S266:	STA	A,SMSD1
	LDA	I,260
S35:	STA	A,SLSD1
	JMP	A,S3580
S268:	LDA	A,SLSD2
	CLC
	ADC	I,2
	CMP	I,72
	BNE	S358
	LDA	A,SMSD2
	CLC
	ADC	I,1
	CMP	I,72
	BNE	S2668
	LDA	A,SHSD2
	CLC
	ADC	I,1
	STA	A,SHSD2
	LDA	I,60
S2668:	STA	A,SMSD2
	LDA	I,60
S358:	STA	A,SLSD2
S3580:	RTS
TRANS:	LDX	I,0
	LDY	I,0
N3:	LDA	ZX,HPOST		;UPDATE CAR POSITIONS
	STA	ZX,HPOS
	LDA	ZX,VPOST
	STA	AY,VPOS
	INX
	INY
	INY
	CPX	I,4
	BNE	N3
	RTS
;
;RAM TEST
;
SLT:	LDX 	I,0
	LDA	I,1
SST10:	STA	AX,2000
	STA	A,TREST
	CMP	AX,2000
	BNE	SST10
SST12:	STA	AX,2400
	STA 	A,TREST
	CMP	AX,2400
	BNE	SST12
SST13:	STA	AX,3000
	STA	A,TREST
	CMP	AX,3000
	BNE	SST13
SST14:	STA	AX,3400
	STA	A,TREST
	CMP	AX,3400
	BNE	SST14
	INX
	BNE	SST10
	CLC
	ADC	I,1
	CMP	I,1
	BNE	SST10
	LDA	I,0
	STA	Z,CRASH
;
;ROM CHECKSUM TEST
;
SST15:	LDX 	I,0
	LDA	I,20
	STA	Z,TEMP2
	LDA	I,5
	STA	Z,TEMP2+1
	LDA	I,0
	STA	Z,LOC
	LDA	I,40
	STA	Z,LOC+1
SST26:	LDY	I,0
	LDA	I,0
	STA	Z,TEMP3
	STA	Z,TEMP1
SST20:	LDA	Z,TEMP1
	CLC
	ADC	NY,LOC
	STA	Z,TEMP1
	INY
	BNE	SST20
	STA	A,TREST
	INC	Z,LOC+1
	INC	Z,TEMP3
	LDA	Z,TEMP3
	CMP	I,4
	BNE	SST20
SST22:	LDA	Z,TEMP1
	CMP	AX,CKSUM
	BEQ	SST25
	LDY	I,0
	TXA
	ORA	I,60
	STA	NY,TEMP2
	INC	Z,TEMP2
SST25:	INX
	CPX	I,8.
	BNE	SST26
	LDA	I,324			;WRITE OUT GAME OPTIONS
	STA	A,2152
	LDA	I,311
	STA	A,2153
	LDA	I,315
	STA	A,2154
	LDA	I,305
	STA	A,2155
	LDA	I,260
	STA	A,2161
SST30:	BIT	A,SPARE
	BVC	QB100
	LDA	I,303
	STA	A,2312
	STA	A,2314
	LDA	I,331
	STA	A,2313
	LDA	I,314
	STA	A,2315
	LDA	I,305
	STA	A,2316
	JMP	QB200
QB100:	LDX	I,0
	LDA	I,40
QB300:	STA	AX,2312
	INX
	CPX	I,5
	BNE	QB300
QB200:	LDA	I,40
	BIT	A,GTIME
	BMI	S,+4
	LDA	I,261
	STA	A,2157
	LDA	I,266
	BIT	A,GTIME
	BMI	QB1
	LDA	I,262
	BVS	S,+4
	LDA	I,265
	BMI	S,+6
QB1:	BVS	S,+4
	LDA	I,271
	STA	A,2160
	LDA	I,212
	STA	Z,TEMP1
	LDA	I,4
	STA	Z,TEMP1+1
	BIT	A,MODE
	BMI	QB2
	BVS	QB3
	LDA	I,MES2+7&377
	STA	Z,TEMP2
	LDA	I,MES2+7&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB3:	LDA	I,MES6+9.&377
	STA	Z,TEMP2
	LDA	I,MES6+9.&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB2:	BVS	QB5
	LDA	I,MES4+7&377
	STA	Z,TEMP2
	LDA	I,MES4+7&177400/256.
	STA	Z,TEMP2+1
	JMP	A,QB4
QB5:	LDA	I,304
	STA	A,2212
	LDA	I,305
	STA	A,2213
	LDA	I,315
	STA	A,2214
	LDA	I,317
	STA	A,2215
	LDX	I,0
	LDA	I,40
QB11:	STA	AX,2216
	INX
	CPX	I,19.
	BNE	QB11
	JMP	A,QB6
QB4:	LDY	I,0
QB7:	LDA	NY,TEMP2
	ORA	I,200
	STA	NY,TEMP1
	INY
	CPY	I,19.
	BNE	QB7
QB6:	BIT	A,BONUS
	BPL	QB12
	LDX	I,0
	LDA	I,40
QB13:	STA	AX,2252
	INX
	CPX	I,13.
	BNE	QB13
	JMP	A,QB9
QB12:	LDA	I,252
	STA	Z,TEMP1
	LDA	I,4
	STA	Z,TEMP1+1
	LDA	I,MES18&377
	STA	Z,TEMP2
	LDA	I,MES18&177400/256.
	STA	Z,TEMP2+1
	LDY	I,0
QB8:	LDA	NY,TEMP2
	ORA	I,200
	STA	NY,TEMP1
	INY
	CPY	I,13.
	BNE	QB8
QB9:	LDA	I,40
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,74
	STA	A,2352
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,75
	STA	A,2353
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,76
	STA	A,2412
	BIT	A,SPARE
	BMI	S,+4
	LDA	I,77
	STA	A,2413
QB10:	BIT	A,SST
	BPL	S,+5
	JMP	A,PWRON
	STA	A,SBO1+1
	STA	A,SBO2+1
	STA	A,SK1
	LDX	I,0
SL1:	LDA	AX,GEARAD
	STA	A,TREST
	BMI	S,+5
	STA	A,SK1+1
	INX
	CPX	I,6
	BNE	SL1
	LDA	A,GP
	BMI	S,+5
	STA	A,SK1+1
	LDA	A,GP+1
	BMI	S,+5
	STA	A,SK1+1
	BIT	A,COINAD
	BMI	S,+5
	STA	A,SK1+1
	BVS	S,+5
	STA	A,SK1+1
	BIT	A,TSEL
	BMI	S,+5
	STA	A,SK1+1
	BIT	A,SBI1
	BMI	S,+5
	STA	A,SK1+1
	BIT	A,SBI2
	BMI	S,+5
	STA	A,SK1+1
	JMP	A,SST30
CKSUM:	.BYTE	314,121,11,221,66,17,72,324,366
	.BYTE	61		;DUMMY BYTE
	.EVEN
TADD:	.WORD	20000,20446,21114
	.WORD	21562,22230,22676
	.WORD	23344,24012,24460
	.WORD 	25126,25574,26242
	.EVEN
MESG:	.WORD	MES0
	.WORD	MES2
	.WORD	MES4
	.WORD	MES6
	.WORD	MES8
	.WORD	MES10
	.WORD	MES12
	.WORD	MES14
	.WORD	MES16
	.WORD	MES18
	.WORD	MES20
	.WORD	MES22
	.WORD	MES24
	.WORD	MES26
	.WORD	MES28
	.WORD	MES22
	.WORD	MES24
	.WORD	MES26
	.WORD	MES28
	.WORD	MES30
POSM:	.WORD	PF+40
	.WORD	PF
	.WORD	PF
	.WORD	PF
	.WORD	PF+1540
	.WORD	PF+1540
	.WORD	PF+300
	.WORD	PF+1540
	.WORD	PF
	.WORD	PF+51
	.WORD	PF+1540
	.WORD	PF+1570
	.WORD	PF+300
	.WORD	PF+1570
	.WORD	PF+1570
	.WORD	PF+1543
	.WORD	PF+1543
	.WORD	PF+1543
	.WORD	PF+1543
	.WORD	PF+113
	.WORD	PF+100
	.WORD	PF+140
	.WORD	PF+200
	.WORD	PF+240
	.WORD	PF+300
	.WORD	PF+340
	.WORD	PF+400
RATIO:	.BYTE	14,10,6,5		;GEAR RATIOS FOR RPM SOUNDS
PRO:	.BYTE	7,4,3
ROOKIE:	.BYTE	3,2,1
NOVICE:	.BYTE	5,3,2
TUP:	.BYTE	46,70,113,136
MAXVEL:	.BYTE	240,220,200,160
MES0:	.ASCII	/ BLACK        TIME        /
	.BYTE	327,310,311,324,305,0
MES2:	.ASCIZ	/       1 COIN PER PLAYER  /
MES4:	.ASCIZ	/       2 COINS PER PLAYER  /
MES6:	.ASCIZ	/    1 OR 2 PLAYERS PER COIN /
MES8:	.ASCIZ	/             RATING/
MES10:	.ASCIZ	/ GREY CARS DRIVE AUTOMATICALLY  /
MES12:	.ASCIZ	/  PUSH BUTTON TO CHANGE TRACKS/
MES14:	.ASCIZ	/       PUSH START BUTTON       /
MES16:	.ASCIZ	/           GAME  OVER  /
MES18:	.ASCIZ	/EXTENDED PLAY/
MES20:	.ASCIZ	/  EXTENDED PLAY FOR   0 POINTS /
MES22:	.ASCIZ	/GRANNY/
MES24:	.ASCIZ	/      12 DIFFERENT TRACKS/
MES26:	.ASCIZ	/ROOKIE/
MES28:	.ASCIZ	/  PRO/
MES30:	.ASCIZ	/KEE  GAMES/
ZEXTRA:	LDA	Z,TEMP1
	SEC
	SBC	I,6
	STA	Z,TEMP1
	LDA	Z,TEMP2
	SEC
	SBC	I,6
	STA	Z,TEMP2
Z1:	BIT	A,COINAD
	BPL	S,+5
	BVC	S,+3
	RTS
	STA	A,TREST
	LDY	I,372
	DEY
	BNE	S,-1
	BIT	A,COINAD
	BVC	S,+8.
	LDA	I,9.
	STA	Z,TEMP1
	BPL	Z2
	LDA	Z,TEMP1
	BEQ	Z2
	DEC	Z,TEMP1
	BNE	Z2
	INC	Z,COINS
Z2:	BIT	A,COINAD
	BPL	S,+8.
	LDA	I,9.
	STA	Z,TEMP2
	BPL	Z1
	LDA	Z,TEMP2
	BEQ	Z1
	DEC	Z,TEMP2
	BNE	Z1
	INC	Z,COINS
	JMP	A,Z1
	.REPT 	37772-.
	.BYTE	0
	.ENDM
.	=37772
	.BYTE	0,60,74,60,74,60
	.END
                                                                                                           